/*
 * Copyright (c) 2021.
 *
 * This file is part of the "Pathfinder2" project, available here:
 * <a href="https://github.com/Wobblyyyy/Pathfinder2">GitHub</a>
 *
 * This project is licensed under the GNU GPL V3 license.
 * <a href="https://www.gnu.org/licenses/gpl-3.0.en.html">GNU GPL V3</a>
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.3.0'
    id 'me.champeau.jmh' version '0.6.6'
}

repositories {
    mavenCentral()
}

project.ext {
    projectName = 'me.wobblyyyy'
    projectVersion = 'v2.3.1'

    jmhTime = '200ms'
    jmhWarmupIterations = 5
    jmhIterations = 5
    jmhWarmupForks = 2
    jmhForks = 2
    jmhTimeUnit = 's'
}

allprojects {
    group = project.projectName
    version = project.projectVersion

    java {
        withJavadocJar()
        withSourcesJar()
    }
}

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()

        prettier([
            'prettier': '2.0.5',
            'prettier-plugin-java': '0.8.0'
        ]).configFile('.prettierrc.yml')
    }

    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
            exclude '**/build/**', '**/build-*/**'
        }

        indentWithSpaces()
        trimTrailingWhitespace()
    }
}

